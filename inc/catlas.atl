include'inc\stdlib.atl';
'main.atl';
include'inc\nav.atl';
size;if'started'; // init code goes here



[1024];dim= 1; // constants
[0.2];hscale= 1;
[0.05];waterLevel= 1;
[30.0];waterFogScale= 1;
[0.0 0.15 0.25];waterColor= 3;


dim;first;2;rep;set'texsize' 2;
img'inc\terrain.bmp';0;dup;shape;[1 2 0];s;ttexsize= 1;
6;[1];ttexsize;0;cat;[4];0;cat;
c''''ret[0] = vec4( a( ivec4( 0, tf.y, 2, 0 ) ),
                    a( ivec4( 0, tf.y, 1, 0 ) ),  
                    a( ivec4( 0, tf.y, 0, 0 ) ), 1.0 );' 1 1 4 0;texture;
dim;first;4;std.fractalNoise;
// get minmax
0;dup;minmax;0;dup;last;1;dup;first;-;e;hspan= 1;first;-1;*;e;hbias= 1;


6;texsize;[1];0;cat;c''''ret[0]=( a( ivec4( tf, 0, 0 ) ) + hbias ) / hspan;' 1 1 1 0;texture;
[150 400];set'lrot' 2;
lrot;first;-300;/;[0 1 0];rot; // rot about y
lrot;last;-300;/;[1 0 0];rot;m; // rot about x
set'lightRotation' 16;

std.nav3d;
[0.5 1.0 2.0];std.origin= 3;
[150 400];std.3dnavParam=;

6;windowSize;[4];0;cat;
c''''ret[ 0 ]=vec4( 0 );' 0 1 4 0; // dummy value to pop off in place of display tensor.


l'started'; // init code complete










std.nav3d;std.3dnavParam;set'lrot' 2;
lrot;first;-300;/;[0 1 0];rot; // rot about y
lrot;last;-300;/;[1 0 0];rot;m; // rot about x
set'lightRotation' 16;



2;dup;2;dup; // duplicate terrain and heightmap
windowSize;set'wdims' 2;
wdims;0;dup;first;1;dup;last;/;0.5;^;e;set'aspect' 1;pop; // set wdims to window dimensions and aspect.





[90];windowSize;0;cat;[0.01 3];0;cat;proj;proj= 16;

depth;backface;
6;dim;dim;*;first;*;windowSize;[4];0;cat;
// Stack here: the arguments (heightmapcopy, gradientcopy, prevFrame), heightMap, gradient
c'
vec4 verts[] = vec4[](
vec4( 0.0,0.0,0.0,1.0 ),
vec4( 1.0,0.0,0.0,1.0 ), 
vec4( 0.0,1.0,0.0,1.0 ), 
vec4( 1.0,1.0,0.0,1.0 ),
vec4( 0.0,1.0,0.0,1.0 ),
vec4( 1.0,0.0,0.0,1.0 ) 
);
out vec3 color;
out vec3 worldPos;
'
int index = gl_VertexID / 6;
int x = index / int( dim );
int y = index - x * int( dim );
vec4 v = vec4( float(x), float(y), 0.0, 0.0 ) + verts[ gl_VertexID % 6 ];
v.z = af( v.xy ).x;
vec2 c = vec2( 0.0, v.z * ttexsize );
color = bf( c ).rgb;
v.xy /= dim;
v.z *= hscale;
worldPos = v.yzx;
ret = proj*std_worldUnrotation*( v.yzxw - vec4( std_origin, 0.0 ) );
'
in vec3 color;
in vec3 worldPos;
'
vec3 d = worldPos - std_origin;
float len = length( d );
float t = ( waterLevel - std_origin.y ) / d.y;
float waterDepth = len * ( 1.0 - t );
float fog = clamp( waterDepth * waterFogScale, 0.0, 1.0 );

ret[0] = vec4( mix( color, waterColor, fog ), 1.0 );
'
3 1 4 0;
depth;backface;




//////////////////////////////////////////////////////////////////
// Help and framerate

std.keyState;[58 59 0];s;first;ifn'skipf1';
'Click and drag: rotate view                     ';e;
'Right click: fly forward                        ';e;0;cat;
'w/s/a/d/c/shift/ctrl/space/q/e: fly around      ';e;0;cat;
'Mouse wheel: change fly speed                   ';e;0;cat;
'Hold f key and drag mouse: change light location';e;0;cat;
std.textToTexture;std.blitText;
l'skipf1';
std.keyState;[59 60 0];s;first;std.gamepadState;[13 14 0];s;first;+;ifn'skipf2';
1;timeDelta;/;toString;e;
std.textToTexture;std.blitText;
l'skipf2';
