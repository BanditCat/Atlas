keys;
0;dup;[41 42 0];s;first;ifn'go'; 
quit;  // Quit if escape pressed.
l'go';
[21 22 0];s;first;ifn'go2'; 
print;load'inc/mandelbrot.atl';  // Reload if r pressed.
l'go2';

size;if'started';
1;1;1;
[1];set'zoom' 1;
[0.5 0];set'offs' 2;
[0 0];set'perturb' 2;
[2];set'exp' 1;
l'started';
pop;pop;pop;

input; // deal with mouse input, a 6 value rank 1 tensor; three axes then three buttons.
[1.1];1;dup;[2 3 0];s;^;get'zoom';*;set'zoom' 1; // set zoom unconditionally
0;dup;[3 4 0];s;first;ifn'nomouse'; // if mouse button 1 not held jump to nomouse
0;dup;[0 2 0];s;windowSize;[1 2 0];s;0;dup;0;cat;/;get'zoom';[4];/;0;dup;0;cat;/;get'offs';+;set'offs' 2;
l'nomouse';
0;dup;[4 5 0];s;first;ifn'nomouse2'; // if mouse button 2 not held jump to nomouse2
0;dup;[0 2 0];s;windowSize;[1 2 0];s;0;dup;0;cat;/;get'zoom';[4];/;0;dup;0;cat;/;
get'perturb';+;set'perturb' 2;
l'nomouse2';
0;dup;[5 6 0];s;first;ifn'nomouse3'; // if mouse button 3 not held jump to nomouse3
0;dup;[1 2 0];s;[400];/;get'exp';+;set'exp' 1;
l'nomouse3';
pop;

windowSize;set'wsize' 2;
get'wsize';
c''
ret[ 0 ] = ( ( t.x + 0.5 ) * 4.0 / wsize.x - 2.0 ) * wsize.x / wsize.y / zoom - offs.x\
ret[ 1 ] = ( ( t.y + 0.5 ) * 4.0 / wsize.y - 2.0 ) / zoom + offs.y\
'0 2;
get'wsize';c''ret[0]=perturb.x\ret[1]=perturb.y\' 0 2;
1;if'skip'; 

l'mand';
3;dup;3;dup;
get'wsize';
c'
vec2 mand( vec2 x, vec2 c, float e ){
  float angle = atan( x.y, x.x ) * e\
  float mag = pow( length( x ), e )\
  return vec2( cos( angle ) * mag + c.x, sin( angle ) * mag + c.y )\
}
float mand2( vec2 x, vec2 c ){
  float count = 0.0\
  vec2 nx = x\
  for( int i = 0\ i < 32\ ++i ){
    nx = mand( nx, c, exp )\
    if( length( nx ) > 4.0 )
      break\
    ++count\
  }
  return count\
}
'
vec2 x = vec2( c( vec4( t.xy, 0.0, 0.0 ) ), d( vec4( t.xy, 0.0, 0.0 ) ) )\
vec2 c = vec2( a( vec4( t.xy, 0.0, 0.0 ) ), b( vec4( t.xy, 0.0, 0.0 ) ) )\
float r = mand2( x, c )\
ret[ 0 ] = r\' 4 1;
return;


l'skip';
mand;
img'inc/font.bmp';
[2];set'fontSize' 1;
'The quick black dog jumps over the lazy brown fox.';0;dup;shape;print;set'wlen' 1;
get'wsize';[3];0;cat;c''
ret[0]= ( t.x < 6.0 * fontSize * wlen && t.y < 8.0 * fontSize ? ( ( mod( floor( t.x / fontSize ), 6.0 ) != 5.0 ?
          b( vec4( mod( floor( t.x / fontSize ), 6.0 ),
	  floor( t.y / fontSize ) + 1328.0 - 8.0 * a( vec4( floor( t.x / ( fontSize * 6.0 ) ), 0.0, 0.0, 0.0 ) )
	  , 1.0, 0.0 ) ) : 1.0 ) ) : 1.0 ) * c( vec4( t.xy, 0.0, 0.0 ) ) / 31.0\
' 3 1;